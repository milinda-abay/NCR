table 'Base Measures'
	lineageTag: c6346c4b-beb3-4238-9876-551a7f514450

	measure FunnelPlotState =
			
			// RLS filter
			LOOKUPVALUE (
			    'Restricted State'[State],
			    'Restricted State'[ID], SELECTEDVALUE ( 'dim Hospital'[State] ),
			    "ALL"
			)
		displayFolder: _Role based
		lineageTag: 238c1dbf-c00a-4ee7-b7c6-f3bde6d459ab

	measure RestrictedHospital =
			
			// RLS filter
			IF (
			    [FunnelPlotState] <> "ALL",
			    SELECTEDVALUE ( 'dim Hospital'[HospitalName] ),
			    "Restricted"
			)
		displayFolder: _Role based
		lineageTag: 4ac0e03c-489b-40d9-b628-0f1b5072af43

	measure RestrictedFlag =
			
			// RLS filter
			VAR Rstatevalue =
			    SELECTEDVALUE ( 'dim State'[STATE] )
			RETURN
			    IF (
			        LOOKUPVALUE (
			            'Restricted State'[State],
			            'Restricted State'[State], Rstatevalue
			        ) <> "",
			        1,
			        BLANK ()
			    )
		formatString: 0
		displayFolder: _Role based
		lineageTag: 5b5859f0-7dcc-4789-8773-6ba812c7af70

	measure CurrentState =
			
			// RLS filter
			VAR selected_state =
			    INTERSECT ( VALUES ( 'dim State'[ID] ), VALUES ( 'Restricted State'[ID] ) )
			VAR result =
			    IF ( SELECTEDVALUE ( 'dim Hospital'[State] ) IN selected_state, "show" )
			RETURN
			    result
		displayFolder: _Role based
		lineageTag: 7d972c11-b2cd-40be-8a43-d9b6e36ea0a4

	measure 'Cardiac Rehospitalisation (%)' =
			
			// Report 7 30-Day Unplanned Events
			VAR Numerator = [Cardiac rehospitalisation]
			VAR Denominator =
			    CALCULATE (
			        [Rows in DER],
			        'fact DER'[dis] <> 6,
			        NOT ( ISBLANK ( 'fact DER'[dis] ) )
			    )
			VAR Result =
			    DIVIDE ( Numerator, Denominator, 0 )
			RETURN
			    Result
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Report 07
		lineageTag: 14f4ec83-6ef4-494c-8e40-bed0dd2e9aab

	measure 'Rows in DER' =
			
			// Common top banner
			VAR table_row_count =
			    COUNTROWS ( 'fact DER' )
			RETURN
			    IF ( ISBLANK ( table_row_count ), 0, table_row_count )
		formatString: 0
		displayFolder: Common top banner
		lineageTag: 1533f4da-e4e2-46a6-b2ed-a98cd2f08e39

	measure Hospitals =
			
			// Common top banner
			VAR FormatValue =
			    DISTINCTCOUNT ( 'fact DER'[ncrhid] )
			RETURN
			    IF ( ISBLANK ( FormatValue ), 0, FormatValue )
		formatString: 0
		displayFolder: Common top banner
		lineageTag: 0c033231-64ff-4a72-8f6e-b1a87861ab8d

	measure Patients =
			
			// Common top banner
			VAR patientCount =
			    DISTINCTCOUNT ( 'fact DER'[ncr_patientId] )
			RETURN
			    IF ( ISBLANK ( patientCount ), 0, patientCount )
		formatString: 0
		displayFolder: Common top banner
		lineageTag: 79dfcc47-7258-4bc6-8dc6-e65762c11337

	measure 'RestrictedHospitalID  (to_delete)' =
			
			// RLS filter
			IF (
			    [FunnelPlotState] <> "ALL",
			    SELECTEDVALUE ( 'dim Hospital'[NCRHospitalID] ),
			    RAND ()
			)
		formatString: 0
		displayFolder: _Role based
		lineageTag: 91af517c-c5b8-4a57-a5ba-e5f1c208db41

	measure 'Peri-PCI stroke (%)' =
			
			// Report 3 In-Hospital Stroke, Major Bleeding and Mortality
			VAR Numerator = [Strokes (ih)]
			VAR Denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( Numerator, Denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 03\Stroke Bleeding
		lineageTag: 76c4c83a-87a6-48d5-b409-bd81e918d0bb

		changedProperty = FormatString

	measure 'Strokes (ih)' =
			
			// Report 3 In-Hospital Stroke, Major Bleeding and Mortality
			VAR num_strokes =
			    CALCULATE ( SUM ( 'fact DER'[ihstr] ) )
			RETURN
			    IF ( ISBLANK ( num_strokes ), 0, num_strokes )
		formatString: 0
		displayFolder: Report 03\Stroke Bleeding
		lineageTag: 08336d23-f132-45f2-9546-b2061bae1716

	measure 'Major bleeding (ih)' =
			
			// Report 3 In-Hospital Stroke, Major Bleeding and Mortality
			VAR num_bleeding =
			    CALCULATE ( [Rows in DER], 'fact DER'[ihbl] IN { 3, 4, 5, 7, 8 } )
			VAR result =
			    IF ( ISBLANK ( num_bleeding ), 0, num_bleeding )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 03\Stroke Bleeding
		lineageTag: 73298a65-cdf9-4058-aa4b-8d730d47c007

	measure 'Major bleeding (%)' =
			
			// Report 3 In-Hospital Stroke, Major Bleeding and Mortality
			VAR Numerator = [Major bleeding (ih)]
			VAR Denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( Numerator, Denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 03\Stroke Bleeding
		lineageTag: 366a4e5c-05e4-4357-8240-c73c78923c7b

		changedProperty = FormatString

	measure Mortality(ih)(N) =
			
			// Report 3 In-Hospital Stroke, Major Bleeding and Mortality
			CALCULATE (
			    [Patients],
			    'fact DER'[ihmort] = 1
			)
		formatString: 0
		displayFolder: Report 03\Mortality
		lineageTag: 2b47f55e-e899-454c-9b6a-0f7d5c68c6f9

	measure Mortality(ih)(%) =
			
			// Report 3 In-Hospital Stroke, Major Bleeding and Mortality
			VAR Numerator = [Mortality(ih)(N)]
			VAR Denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( Numerator, Denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 03\Mortality
		lineageTag: 0c24258d-e336-4d5e-90b9-519ad2db2861

		changedProperty = FormatString

	measure 'Mortality(ih) without shock/ohca (N)' =
			
			// Report 3 In-Hospital Stroke, Major Bleeding and Mortality
			CALCULATE (
			    [Patients],
			    'fact DER'[ihmort] = 1,
			    'fact DER'[shocksohca] = 0
			        || ISBLANK ( 'fact DER'[shocksohca] )
			)
		formatString: 0
		displayFolder: Report 03\Mortality
		lineageTag: 37fbeef3-b665-4813-b6df-292ce7a8c018

	measure 'Mortality(ih) without shock/ohca (%)' =
			
			// Report 3 In-Hospital Stroke, Major Bleeding and Mortality
			VAR numerator = [Mortality(ih) without shock/ohca (N)]
			VAR denominator = [Rows in DER without shock/ohca (D)]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 03\Mortality
		lineageTag: 0819adc1-bace-4eb1-b4cb-1efa59ea641b

		changedProperty = FormatString

	measure 'Unplanned revascularisation' =
			
			// Report 7 30-Day Unplanned Events
			VAR revascularisations =
			    CALCULATE ( [Rows in DER], 'fact DER'[unprevasc30new] = 1 )
			RETURN
			    IF ( ISBLANK ( revascularisations ), 0, revascularisations )
		formatString: 0
		displayFolder: Report 07
		lineageTag: bb50cdf2-4b10-4169-a710-a57bbb1540ef

	measure 'Unplanned revascularisation (%)' =
			
			// Report 7 30-Day Unplanned Events
			VAR Numerator = [Unplanned revascularisation]
			VAR Denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( Numerator, Denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 07
		lineageTag: 2b0632fe-a317-45ff-b027-266f5626f89a

	measure 'Mortality 30 day' =
			
			// Report 8 30-Day Mortality
			VAR numerator =
			    CALCULATE ( [Patients], 'fact DER'[mort30] = 1 )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 08
		lineageTag: 30a0e136-0b51-4438-a80b-870c1f785de8

	measure 'Cardiac rehospitalisation' =
			
			// Report 7 30-Day Unplanned Events
			CALCULATE (
			    [Rows in DER],
			    'fact DER'[dis] <> 6,
			    'fact DER'[crh30any] = 1,
			    'fact DER'[upc30any] = 1
			)
		formatString: 0
		displayFolder: Report 07
		lineageTag: e8b90bc9-baf0-4ad4-b9e4-5e3780190884

	measure 'Admissions(doa) (to_delete)' =
			
			COUNTROWS (
			    SUMMARIZE ( 'fact DER', 'fact DER'[ncr_patientId], 'fact DER'[doa] )
			)
		formatString: 0
		isHidden
		lineageTag: 2900a565-7441-4651-87d9-b34c1a17fe8e

		changedProperty = IsHidden

	measure 'Procedures(dop) (to_delete)' =
			
			COUNTROWS (
			    SUMMARIZE ( 'fact DER', 'fact DER'[ncr_patientId], 'fact DER'[dop] )
			)
		formatString: 0
		isHidden
		lineageTag: cc1cef3c-9027-497b-b752-ec03cf0ea4a1

		changedProperty = IsHidden

	measure 'Rows in DER without shock/ohca (D)' =
			
			// Report 3 In-Hospital Stroke, Major Bleeding and Mortality
			// Report 4 In-Hospital MACE & MACCE
			// Report 8 30-Day Mortality
			// Report 9 30-Day MACE & MACCE
			VAR denominator =
			    CALCULATE (
			        [Rows in DER],
			        'fact DER'[shocksohca] = 0
			            || ISBLANK ( 'fact DER'[shocksohca] )
			    )
			VAR result =
			    IF ( ISBLANK ( denominator ), 0, denominator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 03;Report 04;Report 08;Report 09
		lineageTag: 6d58953b-8f84-4fb2-aa67-a785dd9df100

	measure 'Mortality 30 day(%)' =
			
			// Report 8 30-Day Mortality
			VAR numerator = [Mortality 30 day]
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator, 0 )
			RETURN
			    result
		formatString: 0.00 %
		displayFolder: Report 08
		lineageTag: 225549e3-0e3c-49fe-b2d8-67716600bb93

	measure 'Mortality 30 day without shock/ohca (N)' =
			
			// Report 8 30-Day Mortality
			VAR numerator =
			    CALCULATE ( [Patients], 'fact DER'[mort30excshocksohca] = 1 )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 08
		lineageTag: 1030c4b6-08a6-4a91-b561-dbad53b9a1b9

	measure 'Mortality 30 day without shock/ohca (%)' =
			
			// Report 8 30-Day Mortality
			VAR numerator = [Mortality 30 day without shock/ohca (N)]
			VAR denominator = [Rows in DER without shock/ohca (D)]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 08
		lineageTag: 3dac72e0-5633-4111-8f1b-3eb6b8d13695

	measure 'MACE 30 day' =
			
			// Report 9 30-Day MACE & MACCE
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[macenew] = 1 )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 09\MACE
		lineageTag: d528f186-4f36-4bc1-978c-76602506ca89

	measure 'MACE 30 day without shock/ohca (N)' =
			
			// Report 9 30-Day MACE & MACCE
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[shocksohca] = 0, 'fact DER'[macenew] = 1 )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 09\MACE
		lineageTag: 456435e7-bf54-4c43-bde7-bd88681f378f

	measure 'MACE 30 day (%)' =
			
			// Report 9 30-Day MACE & MACCE
			VAR numerator = [MACE 30 day]
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), BLANK (), result )
		formatString: 0.00 %
		displayFolder: Report 09\MACE
		lineageTag: 1e329476-129e-4bd4-a979-3ccd81d6f0fe

	measure 'MACE 30 day without shock/ohca(%)' =
			
			// Report 9 30-Day MACE & MACCE
			VAR numerator = [MACE 30 day without shock/ohca (N)]
			VAR denominator = [Rows in DER without shock/ohca (D)]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), BLANK (), result )
		formatString: 0.00 %
		displayFolder: Report 09\MACE
		lineageTag: 67f40371-927e-4ec2-a3dd-ef8c97e4b20e

	measure 'MACCE 30 day' =
			
			// Report 9 30-Day MACE & MACCE
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[maccenew] = 1 )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 09\MACCE
		lineageTag: c7fe035f-d0c8-49c6-8326-9a2a20994097

	measure 'MACCE 30 day (%)' =
			
			// Report 9 30-Day MACE & MACCE
			VAR numerator = [MACCE 30 day]
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), BLANK (), result )
		formatString: 0.00 %
		displayFolder: Report 09\MACCE
		lineageTag: bf5f9659-199c-4077-af8a-d1f2a947a844

	measure 'MACCE 30 day without shock/ohca (N)' =
			
			// Report 9 30-Day MACE & MACCE
			VAR numerator =
			    CALCULATE (
			        [Rows in DER],
			        'fact DER'[shocksohca] = 0,
			        'fact DER'[maccenew] = 1
			    )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 09\MACCE
		lineageTag: f862ab68-799b-4e42-9314-5401975b1578

	measure 'MACCE 30 day without shock/ohca(%)' =
			
			// Report 9 30-Day MACE & MACCE
			VAR numerator = [MACCE 30 day without shock/ohca (N)]
			VAR denominator = [Rows in DER without shock/ohca (D)]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), BLANK (), result )
		formatString: 0.00 %
		displayFolder: Report 09\MACCE
		lineageTag: 19977a80-2898-4f8c-997c-38620fe3a339

	measure 'MACCE (ih)' =
			
			// Report 4 In-Hospital MACE & MACCE
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[ihmaccenew] = 1 )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 04\MACCE
		lineageTag: 60568474-0cde-4fc2-a3ce-e98d2f55cdd7

	measure 'MACCE (ih) (%)' =
			
			// Report 4 In-Hospital MACE & MACCE
			VAR numerator = [MACCE (ih)]
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 04\MACCE
		lineageTag: ebd4a20b-af41-44da-b287-22225a81bd60

	measure 'MACCE (ih) without shock/ohca (N)' =
			
			// Report 4 In-Hospital MACE & MACCE
			VAR numerator =
			    CALCULATE (
			        [Rows in DER],
			        'fact DER'[shocksohca] = 0,
			        'fact DER'[ihmaccenew] = 1
			    )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 04\MACCE
		lineageTag: e2739680-77a4-4e5b-8ca7-fb860f1c4490

	measure 'MACCE (ih) without shock/ohca(%)' =
			
			// Report 4 In-Hospital MACE & MACCE
			VAR numerator = [MACCE (ih) without shock/ohca (N)]
			VAR denominator = [Rows in DER without shock/ohca (D)]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 04\MACCE
		lineageTag: c9dd7cbd-5676-4c79-8f2f-eeabeecaabb8

	measure 'MACE (ih)' =
			
			// Report 4 In-Hospital MACE & MACCE
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[ihmacenew] = 1 )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 04\MACE
		lineageTag: 0fd0859f-cc03-46b1-a67d-e565d17a96ad

	measure 'MACE (ih) without shock/ohca (N)' =
			
			// Report 4 In-Hospital MACE & MACCE
			VAR numerator =
			    CALCULATE (
			        [Rows in DER],
			        'fact DER'[shocksohca] = 0,
			        'fact DER'[ihmacenew] = 1
			    )
			VAR result =
			    IF ( ISBLANK ( numerator ), 0, numerator )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 04\MACE
		lineageTag: 58c9017f-62dc-4654-8005-54bdcff3e779

	measure 'MACE (ih) (%)' =
			
			// Report 4 In-Hospital MACE & MACCE
			VAR numerator = [MACE (ih)]
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 04\MACE
		lineageTag: c21ed068-cff1-40f3-aaef-cd71bf4d8fe6

	measure 'MACE (ih) without shock/ohca(%)' =
			
			// Report 4 In-Hospital MACE & MACCE
			VAR numerator = [MACE (ih) without shock/ohca (N)]
			VAR denominator = [Rows in DER without shock/ohca (D)]
			VAR result =
			    DIVIDE ( numerator, denominator, BLANK () )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.00 %
		displayFolder: Report 04\MACE
		lineageTag: d83b68f0-00c0-443c-a162-87f7bcc9b9d0

	measure alllesionsuccess =
			
			// Report 12 Lesion Location & Lesion Procedure Success
			VAR a_table =
			    CALCULATETABLE (
			        'fact DER',
			        'fact DER'[lr1_lst] = 1,
			        'fact DER'[lr2_lst] = 1
			            || ISBLANK ( 'fact DER'[lr2_lst] ),
			        'fact DER'[lr3_lst] = 1
			            || ISBLANK ( 'fact DER'[lr3_lst] ),
			        'fact DER'[lr4_lst] = 1
			            || ISBLANK ( 'fact DER'[lr4_lst] ),
			        'fact DER'[lr5_lst] = 1
			            || ISBLANK ( 'fact DER'[lr5_lst] )
			    )
			VAR a_normal =
			    CALCULATE (
			        [Rows in DER],
			        'fact DER'[lr1_lst] = 1,
			        'fact DER'[lr2_lst] = 1
			            || ISBLANK ( 'fact DER'[lr2_lst] ),
			        'fact DER'[lr3_lst] = 1
			            || ISBLANK ( 'fact DER'[lr3_lst] ),
			        'fact DER'[lr4_lst] = 1
			            || ISBLANK ( 'fact DER'[lr4_lst] ),
			        'fact DER'[lr5_lst] = 1
			            || ISBLANK ( 'fact DER'[lr5_lst] )
			    )
			RETURN
			    a_normal
		formatString: 0
		displayFolder: Report 12
		lineageTag: 527009e9-8b0c-44c8-bf73-0bdf8683c2d6

	measure alllesionsucess(%) =
			
			// Report 12 Lesion Location & Lesion Procedure Success
			VAR calc =
			    DIVIDE ( [alllesionsuccess], [Rows in DER], 0 )
			VAR result =
			    IF ( ISBLANK ( calc ), 0, calc )
			RETURN
			    result
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Report 12
		lineageTag: 8238d0fa-42f8-4e40-8656-90de14cbf2b3

	measure procedure_success =
			
			// Report 12 Lesion Location & Lesion Procedure Success
			VAR alllesion_table =
			    CALCULATETABLE (
			        'fact DER',
			        'fact DER'[lr1_lst] = 1,
			        'fact DER'[lr2_lst] = 1
			            || ISBLANK ( 'fact DER'[lr2_lst] ),
			        'fact DER'[lr3_lst] = 1
			            || ISBLANK ( 'fact DER'[lr3_lst] ),
			        'fact DER'[lr4_lst] = 1
			            || ISBLANK ( 'fact DER'[lr4_lst] ),
			        'fact DER'[lr5_lst] = 1
			            || ISBLANK ( 'fact DER'[lr5_lst] )
			    )
			VAR sigcomplication_table =
			    CALCULATETABLE ( 'fact DER', 'fact DER'[sigcomplication] = 0 )
			VAR result =
			    COUNTROWS ( INTERSECT ( alllesion_table, sigcomplication_table ) )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 12
		lineageTag: 548b37df-cfc2-48de-b9cb-42570fcb6012

	measure 'procedure_success (%)' =
			
			// Report 12 Lesion Location & Lesion Procedure Success
			VAR calc =
			    DIVIDE ( [procedure_success], [Rows in DER] )
			VAR result =
			    IF ( ISBLANK ( calc ), 0, calc )
			RETURN
			    result
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Report 12
		lineageTag: e03939f9-6749-4622-9668-2e4350461e2a

	measure 'Report 1 Hospital' =
			
			// Report 1 Primary PCI for STEMI FMC-ECG
			CALCULATE (
			    [Hospitals],
			    'fact DER'[pci] = 1,
			    NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			    'fact DER'[iht] = 0,
			    'fact DER'[inp] = 0
			        || ISBLANK ( 'fact DER'[inp] ),
			    'fact DER'[s2d] > 0,
			    'fact DER'[s2d] < 720,
			    NOT ( ISBLANK ( 'fact DER'[s2d] ) )
			)
		formatString: 0
		displayFolder: Report 01
		lineageTag: 2c7426ef-a6c3-4ae9-ad37-b3eca4d2f23b

	measure 'Report 1 Patient' =
			
			// Report 1 Primary PCI for STEMI FMC-ECG
			CALCULATE (
			    [Patients],
			    'fact DER'[pci] = 1,
			    NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			    'fact DER'[iht] = 0,
			    'fact DER'[inp] = 0
			        || ISBLANK ( 'fact DER'[inp] ),
			    'fact DER'[s2d] > 0,
			    'fact DER'[s2d] < 720,
			    NOT ( ISBLANK ( 'fact DER'[s2d] ) )
			)
		formatString: 0
		displayFolder: Report 01
		lineageTag: 8cc19a75-2514-4ab1-904a-6364543e28da

	measure 'Report 1 PCI' =
			
			// Report 1 Primary PCI for STEMI FMC-ECG
			CALCULATE (
			    [Rows in DER],
			    'fact DER'[pci] = 1,
			    NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			    'fact DER'[iht] = 0,
			    'fact DER'[inp] = 0
			        || ISBLANK ( 'fact DER'[inp] ),
			    'fact DER'[s2d] > 0,
			    'fact DER'[s2d] < 720,
			    NOT ( ISBLANK ( 'fact DER'[s2d] ) )
			)
		formatString: 0
		displayFolder: Report 01
		lineageTag: cddf671d-2828-4eb0-9c9c-59394327befa

	measure 'Median FMC to reperfusion' =
			
			// Report 1 Primary PCI for STEMI FMC-ECG
			VAR filter_table =
			    CALCULATETABLE (
			        'fact DER',
			        'fact DER'[pci] = 1,
			        NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			        'fact DER'[iht] = 0,
			        'fact DER'[inp] = 0
			            || ISBLANK ( 'fact DER'[inp] ),
			        'fact DER'[s2d] > 0,
			        'fact DER'[s2d] < 720,
			        NOT ( ISBLANK ( 'fact DER'[s2d] ) ),
			        'fact DER'[fmctr] > 0
			    )
			VAR fmctr_300_table =
			    ADDCOLUMNS (
			        filter_table,
			        "fmctr_300", IF ( 'fact DER'[fmctr] > 300, 300, 'fact DER'[fmctr] )
			    )
			VAR new_result =
			    CALCULATE ( MEDIANX ( fmctr_300_table, [fmctr_300] ), filter_table )
			RETURN
			    new_result
		displayFolder: Report 01
		lineageTag: cbd550a3-62dc-48f3-ace8-2031c7b24ef2

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Median ECG to reperfusion' =
			
			// Report 1 Primary PCI for STEMI FMC-ECG
			VAR base_table =
			    CALCULATETABLE (
			        'fact DER',
			        'fact DER'[pci] = 1,
			        NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			        'fact DER'[iht] = 0,
			        'fact DER'[inp] = 0
			            || ISBLANK ( 'fact DER'[inp] ),
			        'fact DER'[s2d] > 0,
			        'fact DER'[s2d] < 720,
			        NOT ( ISBLANK ( 'fact DER'[s2d] ) ),
			        'fact DER'[ecgdb] > 0
			    )
			VAR new_table =
			    ADDCOLUMNS (
			        base_table,
			        "ecgdb_300", IF ( 'fact DER'[ecgdb] > 300, 300, 'fact DER'[ecgdb] )
			    )
			VAR new_result =
			    CALCULATE ( MEDIANX ( new_table, [ecgdb_300] ), base_table )
			RETURN
			    new_result
		formatString: 0.0
		displayFolder: Report 01
		lineageTag: 56295d74-f1e8-4537-97be-ca55e2aef4c2

	measure 'Report 2 Hospital' =
			
			// Report 2 Primary PCI for STEMI DBT ≤ 90 minutes
			CALCULATE (
			    [Hospitals],
			    'fact DER'[pci] = 1,
			    'fact DER'[iht] = 0,
			    NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			    'fact DER'[inp] = 0,
			    'fact DER'[s2d] >= 0,
			    'fact DER'[s2d] < 721,
			    NOT ( ISBLANK ( 'fact DER'[s2d] ) ),
			    'fact DER'[dbdt] > 0
			)
		formatString: 0
		displayFolder: Report 02
		lineageTag: fd2a5b2b-7bc2-4b02-bcd9-1d3a3204e604

	measure 'Report 2 Patient' =
			
			// Report 2 Primary PCI for STEMI DBT ≤ 90 minutes
			CALCULATE (
			    [Patients],
			    'fact DER'[pci] = 1,
			    'fact DER'[iht] = 0,
			    NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			    'fact DER'[inp] = 0,
			    'fact DER'[s2d] >= 0,
			    'fact DER'[s2d] < 721,
			    NOT ( ISBLANK ( 'fact DER'[s2d] ) ),
			    'fact DER'[dbdt] > 0
			)
		formatString: 0
		displayFolder: Report 02
		lineageTag: a6838307-1ef3-4e2b-83d8-3b3a61bd03d0

	measure 'Report 2 PCI' =
			
			// Report 2 Primary PCI for STEMI DBT ≤ 90 minutes
			CALCULATE (
			    [Rows in DER],
			    'fact DER'[pci] = 1,
			    'fact DER'[iht] = 0,
			    NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			    'fact DER'[inp] = 0,
			    'fact DER'[s2d] >= 0,
			    'fact DER'[s2d] < 721,
			    NOT ( ISBLANK ( 'fact DER'[s2d] ) ),
			    'fact DER'[dbdt] > 0
			)
		formatString: 0
		displayFolder: Report 02
		lineageTag: b58748b1-3293-4c35-8a3c-eb42f3714893

	measure 'Report 2 dbdt <90' =
			
			// Report 2 Primary PCI for STEMI DBT ≤ 90 minutes
			CALCULATE (
			    [Rows in DER],
			    'fact DER'[pci] = 1,
			    'fact DER'[iht] = 0,
			    NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			    'fact DER'[inp] = 0,
			    'fact DER'[s2d] >= 0,
			    'fact DER'[s2d] < 721,
			    NOT ( ISBLANK ( 'fact DER'[s2d] ) ),
			    'fact DER'[dbdt] > 0,
			    'fact DER'[dbdt] <= 90
			)
		formatString: 0
		displayFolder: Report 02
		lineageTag: b2a5ca15-830c-471e-a2ca-8326d3d5a2ed

	measure 'Cases <90 min Door' =
			
			// Report 2 Primary PCI for STEMI DBT ≤ 90 minutes
			DIVIDE (
			    [Report 2 dbdt <90],
			    [Report 2 PCI],
			    0
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Report 02
		lineageTag: b78d12a6-3215-4a1d-bf33-6556691064ac

	measure 'Count hospitals meet target' =
			
			// Report 2 Primary PCI for STEMI DBT ≤ 90 minutes
			VAR base_table =
			    ADDCOLUMNS (
			        SUMMARIZE (
			            CALCULATETABLE (
			                'fact DER',
			                'fact DER'[pci] = 1,
			                'fact DER'[iht] = 0,
			                NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			                'fact DER'[inp] = 0,
			                'fact DER'[s2d] >= 0,
			                'fact DER'[s2d] < 721,
			                NOT ( ISBLANK ( 'fact DER'[s2d] ) ),
			                'fact DER'[dbdt] > 0
			            ),
			            'fact DER'[ncrhid]
			        ),
			        "cases", [Cases <90 min Door]
			    )
			VAR filter_table =
			    FILTER ( base_table, [cases] > .75 )
			VAR result =
			    COUNTROWS ( filter_table )
			RETURN
			    result
		formatString: 0
		displayFolder: Report 02
		lineageTag: 475a53ea-1715-40c7-9d38-cd4ca85472c5

	measure 'Report 2 Median dbdt' =
			
			// Report 2 Primary PCI for STEMI DBT ≤ 90 minutes
			VAR base_table =
			    CALCULATETABLE (
			        'fact DER',
			        'fact DER'[pci] = 1,
			        'fact DER'[iht] = 0,
			        NOT ( ISBLANK ( 'fact DER'[iht] ) ),
			        'fact DER'[inp] = 0,
			        'fact DER'[s2d] >= 0,
			        'fact DER'[s2d] < 721,
			        NOT ( ISBLANK ( 'fact DER'[s2d] ) ),
			        'fact DER'[dbdt] > 0
			    )
			VAR new_table =
			    ADDCOLUMNS (
			        base_table,
			        "dbdt_300", IF ( 'fact DER'[dbdt] > 300, 300, 'fact DER'[dbdt] )
			    )
			VAR new_result =
			    CALCULATE ( MEDIANX ( new_table, [dbdt_300] ), base_table )
			RETURN
			    new_result
		displayFolder: Report 02
		lineageTag: 7edcdd1b-b5df-4b93-ba32-e74028ce2f3d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Target 75% ≤ 90' =
			
			// Report 2 Primary PCI for STEMI DBT ≤ 90 minutes
			IF (
			    [Cases <90 min Door] = 0,
			    BLANK (),
			    0.75
			)
		displayFolder: Report 02
		lineageTag: 0114436f-abd6-4d5d-a27a-0daa89ad81e7

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure crehab_count =
			
			// Report 6 Rehab Referral
			CALCULATE (
			    [Rows in DER],
			    'fact DER'[crehab] = 1,
			    'fact DER'[dis] < 6
			)
		formatString: 0
		displayFolder: Report 06
		lineageTag: 4e5bd2f1-e2c7-45af-8783-1541fa7313a9

	measure crehab_denom =
			
			// Report 6 Rehab Referral
			CALCULATE (
			    [Rows in DER],
			    NOT ( ISBLANK ( 'fact DER'[crehab] ) )
			)
		formatString: 0
		displayFolder: Report 06
		lineageTag: 035573d5-5e32-4838-a16a-1b0666006960

	measure 'Cardiac Rehabilitation' =
			
			// Report 6 Rehab Referral
			DIVIDE ( [crehab_count], [crehab_denom] )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Report 06
		lineageTag: 9de3798e-f58b-4557-b3c1-ace5109921c5

	measure 'Mean LOS' =
			
			// Report 13 Non-ACS and NSTEMI Length of stay (LOS)
			VAR base_table =
			    CALCULATETABLE ( 'fact DER', NOT ( ISBLANK ( 'fact DER'[conditional_los] ) ) )
			VAR result =
			    CALCULATE ( AVERAGE ( 'fact DER'[conditional_los] ), base_table )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.000
		displayFolder: Report13
		lineageTag: 83a37708-47c2-410c-b5dc-8bb955effe18

	measure 'Report 5 PCI (lltgp)' =
			
			// Report 5 Discharge Medication
			CALCULATE (
			    [Rows in DER],
			    NOT ( ISBLANK ( 'fact DER'[dstp] ) ) || NOT ( ISBLANK ( 'fact DER'[doll] ) )
			)
		formatString: 0
		displayFolder: Report 05
		lineageTag: 71e9f341-7f87-4ae6-9b72-e21105e9a5b5

	measure lltpg =
			
			// Report 5 Discharge Medication
			CALCULATE (
			    [Rows in DER],
			    'fact DER'[dis] < 6,
			    'fact DER'[dstp] = 1
			        || 'fact DER'[doll] = 1
			)
		formatString: 0
		displayFolder: Report 05
		lineageTag: 6d7792ef-070c-4664-a97e-68215ef3c5b4

	measure 'lltgp (%)' =
			
			// Report 5 Discharge Medication
			DIVIDE ( [lltpg], [Report 5 PCI (lltgp)] )
		formatString: 0.00000%;-0.00000%;0.00000%
		displayFolder: Report 05
		lineageTag: d1d40a75-1afe-433a-8165-3639b4f6b5ce

	measure 'Report 5 PCI (daptgp)' =
			
			// Report 5 Discharge Medication
			CALCULATE (
			    [Rows in DER],
			    NOT ( ISBLANK ( 'fact DER'[dasp] ) ) || NOT ( ISBLANK ( 'fact DER'[doap] ) )
			)
		formatString: 0
		displayFolder: Report 05
		lineageTag: 4c5a7135-8e24-413c-943d-ccc0d9cfe784

	measure daptgp =
			
			// Report 5 Discharge Medication
			CALCULATE (
			    [Rows in DER],
			    'fact DER'[dis] < 6,
			    'fact DER'[dasp] = 1
			        || 'fact DER'[doap] = 1
			)
		formatString: 0
		displayFolder: Report 05
		lineageTag: f50f3e58-d37b-477d-955f-ae953c52682f

	measure 'daptgp (%)' =
			
			// Report 5 Discharge Medication
			DIVIDE ( [daptgp], [Report 5 PCI (daptgp)] )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Report 05
		lineageTag: 5a4cb8b4-0beb-4afe-9679-ec7fe13b5183

	measure 'Gender -female' =
			
			// Report 10 Risk factors
			VAR num_female =
			    CALCULATE ( [Rows in DER], 'dim Sex'[ID] = 2 )
			VAR result =
			    DIVIDE ( num_female, [Rows in DER] ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 79c1b5e2-b204-444c-851f-0ec6d0001106

	measure 'Age - Mean' =
			
			// Report 10 Risk factors
			AVERAGE ( 'fact DER'[age] )
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: b7c66a24-8375-4733-ab0e-8779587b60c9

	measure 'Age - Median' =
			
			// Report 10 Risk factors
			MEDIAN ( 'fact DER'[age] )
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 5a2b172d-7ce4-4a6d-8cb1-7c9eb837538d

	measure 'Previous PCI (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[ppci] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: de065473-579a-47bd-8a6e-dc1f2f509290

	measure 'BMIUnderweight (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'dim BMI_Groups'[bmi_id] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 65bf8883-b2d1-4bce-aba5-8bd5207af4dc

	measure 'BMINormal (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'dim BMI_Groups'[bmi_id] = 2 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 8312f722-59d0-4e11-8480-67c30d840e02

	measure 'BMIOverweight (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'dim BMI_Groups'[bmi_id] = 3 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 662972dc-8355-47ed-ac26-89f37538a217

	measure 'BMIObese (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'dim BMI_Groups'[bmi_id] = 4 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 8832ab62-f234-4a9c-bb6e-87ceecb34e1c

	measure EGFRGreaterThan60 =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[egfr] > 60 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 72d4ce2d-6927-45cc-a124-ebbe280a5c77

	measure EGFRBetween31And60 =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[egfr] >= 31, 'fact DER'[egfr] <= 60 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 1195ace0-0f47-4c51-a9f3-5f6704587c0b

	measure EGFRLessThanThirty =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE (
			        [Rows in DER],
			        'fact DER'[egfr] <= 30,
			        NOT ( ISBLANK ( 'fact DER'[egfr] ) )
			    )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: cf8b905c-f936-4318-99c9-3c326cadb61b

	measure 'EFNormal (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[ef] >= 50 || 'fact DER'[efes] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: ce543740-fa90-4ab2-9242-23d647755e60

	measure 'EFMild (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE (
			        [Rows in DER],
			        ( 'fact DER'[ef] >= 45
			            && 'fact DER'[ef] < 50 )
			            || 'fact DER'[efes] = 2
			    )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 30787239-c232-4b9f-8463-1e0afaaaa900

	measure 'EFModerate (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE (
			        [Rows in DER],
			        ( 'fact DER'[ef] >= 35
			            && 'fact DER'[ef] < 45 )
			            || 'fact DER'[efes] = 3
			    )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 181d74a6-277a-494f-bc8b-16734e2a8c6b

	measure 'EFSevere (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE (
			        [Rows in DER],
			        (
			            NOT ( ISBLANK ( 'fact DER'[ef] ) )
			                && 'fact DER'[ef] < 35
			        )
			            || 'fact DER'[efes] = 4
			    )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: f104c0eb-d481-46fd-aa02-5595afead919

	measure 'Cardiogenic Shock (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[shock] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 9bf68129-1a2e-4256-8a41-328a294b40d8

	measure 'Out of hospital cardiac arrest (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[oca] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 94fd5cc4-5fed-4426-b7fa-417a07fb7642

	measure 'Preprocedural intubation (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[pint] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: ea1709bf-9936-4822-9e40-027f4ce53c9e

	measure 'Procedural Intubation required (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[pintr] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 71a5197a-8274-426a-b11a-92c0a4f0a81b

	measure Diabetes =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[db] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 29b3d2fa-2da8-4754-b719-6f8aa3050091

	measure 'Previous CABG (%)' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[pcabg] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 35de2b5c-019f-4437-84c9-eb1ecf75c344

	measure 'PVD History' =
			
			// Report 10 Risk factors
			VAR numerator =
			    CALCULATE ( [Rows in DER], 'fact DER'[pvd1] = 1 || 'fact DER'[pvd2] = 1 )
			VAR denominator = [Rows in DER]
			VAR result =
			    DIVIDE ( numerator, denominator ) * 100
			RETURN
			    result
		formatString: 0.0
		displayFolder: Report 10
		lineageTag: 2eaaf921-7523-4811-9ca5-438bd5a5dc24

	measure 'AGE (Years)' =
			
			// Report 10 Risk factors
			BLANK ()
		formatString: 0
		displayFolder: Report 10
		lineageTag: 9f0adb4e-64c0-4822-a7a2-e34768eb30bb

	measure PROFILE =
			
			// Report 10 Risk factors
			BLANK ()
		formatString: 0
		displayFolder: Report 10
		lineageTag: aa6572af-8ce1-499d-84d1-07c6a7bab5ee

	measure 'BMI (kg/m2)' =
			
			// Report 10 Risk factors
			BLANK ()
		formatString: 0
		displayFolder: Report 10
		lineageTag: 7605d8cb-08b0-48b2-b6b8-b3235e11f161

	measure 'EGFR (ml/min/1.73 m2)' =
			
			// Report 10 Risk factors
			BLANK ()
		formatString: 0
		displayFolder: Report 10
		lineageTag: ef6d23f1-fdb0-4dd7-bc83-6ee4ecf8852a

	measure LVEF =
			
			// Report 10 Risk factors
			BLANK ()
		formatString: 0
		displayFolder: Report 10
		lineageTag: 07f5d5ca-571b-470a-9a11-bf9a51954763

	measure OTHER =
			
			// Report 10 Risk factors
			BLANK ()
		formatString: 0
		displayFolder: Report 10
		lineageTag: dc990d86-7d73-4dff-8ffa-7f76c0e62e97

	measure 'Femoral %' =
			
			// Report 11 Access Route
			VAR numerator =
			    CALCULATE (
			        [Rows in DER],
			        NOT ( ISBLANK ( 'fact DER'[access_route] ) ),
			        'fact DER'[access_route] IN { "Femoral" }
			    )
			VAR denominator =
			    CALCULATE ( [Rows in DER], NOT ( 'fact DER'[access_route] IN { "Blank pel" } ) )
			VAR result =
			    DIVIDE ( numerator, denominator, 0 )
			RETURN
			    result
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Report 11
		lineageTag: 6917602b-6b52-418e-a4b2-dc4a03d804ce

	measure 'Radial %' =
			
			// Report 11 Access Route
			VAR numerator =
			    CALCULATE (
			        [Rows in DER],
			        NOT ( ISBLANK ( 'fact DER'[access_route] ) ),
			        'fact DER'[access_route] IN { "Radial" }
			    )
			VAR denominator =
			    CALCULATE ( [Rows in DER], NOT ( 'fact DER'[access_route] IN { "Blank pel" } ) )
			VAR result =
			    DIVIDE ( numerator, denominator, 0 )
			RETURN
			    result
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Report 11
		lineageTag: a0d73e55-6c55-4780-9883-525815d94358

	measure 'non ACS cases' =
			
			// Report 13 Non-ACS and NSTEMI Length of stay (LOS)
			CALCULATE (
			    [Rows in DER],
			    NOT ( ISBLANK ( 'fact DER'[los] ) ),
			    'fact DER'[dis] <> 6,
			    'fact DER'[acs] = 0
			)
		formatString: 0
		displayFolder: Report13
		lineageTag: 1388e6d7-8f3c-409c-be90-ddc8172a7b5e

	measure 'Mean Non ACS LOS (Days)' =
			
			// Report 13 Non-ACS and NSTEMI Length of stay (LOS)
			VAR base_table =
			    CALCULATETABLE (
			        'fact DER',
			        NOT ( ISBLANK ( 'fact DER'[los] ) ),
			        'fact DER'[dis] <> 6,
			        'fact DER'[acs] = 0
			    )
			VAR result =
			    CALCULATE ( AVERAGE ( 'fact DER'[los] ), base_table )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.0
		displayFolder: Report13
		lineageTag: 19348f89-2199-4b5d-a9f0-4195fd8f2b3e

	measure 'NSTEMI cases' =
			
			// Report 13 Non-ACS and NSTEMI Length of stay (LOS)
			CALCULATE (
			    [Rows in DER],
			    NOT ( ISBLANK ( 'fact DER'[los] ) ),
			    'fact DER'[dis] <> 6,
			    'fact DER'[acst] = 2
			)
		formatString: 0
		displayFolder: Report13
		lineageTag: c3886bdf-9ccd-4adb-b32d-e0d9cb34004c

	measure 'Mean NSTEMI LOS (Days)' =
			
			// Report 13 Non-ACS and NSTEMI Length of stay (LOS)
			VAR base_table =
			    CALCULATETABLE (
			        'fact DER',
			        NOT ( ISBLANK ( 'fact DER'[los] ) ),
			        'fact DER'[dis] <> 6,
			        'fact DER'[acst] = 2
			    )
			VAR result =
			    CALCULATE ( AVERAGE ( 'fact DER'[los] ), base_table )
			RETURN
			    IF ( ISBLANK ( result ), 0, result )
		formatString: 0.0
		displayFolder: Report13
		lineageTag: f76bcdbb-0a3b-4c11-9e9f-73fcc04965c6

	partition 'Base Measures-519b2a72-add6-4fde-9934-8654fa724d2a' = m
		mode: import
		queryGroup: 'Dimension tables'
		source =
				let
				    Source = #table({}, {})
				in
				    Source

	annotation PBI_ResultType = Table

